programa parcial
procesos    
  proceso juntarF (ES f: numero)
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      f:= f + 1
  fin
  proceso enviarID
  comenzar
    EnviarMensaje (1, r1)
    EnviarMensaje (2, r2)
    EnviarMensaje (3, r3)
  fin
  proceso calcularMax (E f: numero; E id: numero ; ES idMax: numero ; ES max: numero)
  comenzar
    si (f > max)
      max:= f
      idMax:= id
  fin
  proceso recorrer (ES f: numero)
  comenzar
    repetir 5
      juntarF (f)
      mover
    juntarF (f)
    derecha
  fin
areas
  c1: AreaP (1,1,6,6)
  c2: AreaP (7,1,12,6)
  c3: AreaP (13,1,18,6)
  jefe: AreaP (20,1,20,1)
robots
  robot tipo1 {TRABAJADORES}
  variables   
    id, id2, f: numero
  comenzar
    f:= 0
    RecibirMensaje (id, coordinador)
    repetir 4
      recorrer (f)
      si (id = 1)
        EnviarMensaje (1,r2)
        EnviarMensaje (1,r3)
      sino
        si (id = 2)
          EnviarMensaje (2,r1)
          EnviarMensaje (2,r3)
        sino
          EnviarMensaje (3,r1)
          EnviarMensaje (3,r2)
      repetir 2
        RecibirMensaje (id2, *)
    EnviarMensaje (id, coordinador)
    EnviarMensaje (f, coordinador)
  fin
  robot tipo2 {COORDINADOR}
  variables   
    id,idMax, f,max: numero
  comenzar
    max:= 0
    enviarID
    repetir 3
      RecibirMensaje (id, *)
      si (id = 1)
        RecibirMensaje (f, r1)
      sino
        si (id = 2)
          RecibirMensaje (f, r2)
        sino
          RecibirMensaje (f, r3)
      calcularMax (f, id, idMax, max)
    Informar ('ganador', idMax)
    Informar ('ConUnaCantidadDe', max) 
  fin
variables
  r1: tipo1
  r2: tipo1
  r3: tipo1
  coordinador: tipo2
comenzar
  AsignarArea (r1,c1)
  AsignarArea (r2,c2)
  AsignarArea (r3,c3)
  AsignarArea (coordinador,jefe)
  Iniciar (r1,1,1)
  Iniciar (r2,7,1)
  Iniciar (r3,13,1)
  Iniciar (coordinador,20,1)
fin 